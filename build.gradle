buildscript {
	ext {
		springBootVersion = '2.0.0.RELEASE'
	}
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}

	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.21"
		classpath 'com.bmuschko:gradle-docker-plugin:3.0.6'
		classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.5"
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.gorylenko.gradle-git-properties'
apply plugin: 'com.bmuschko.docker-java-application'
apply plugin: 'org.sonarqube'
apply plugin: "jacoco"

def appVersion =  System.properties['appVersion']?:'0.0.1-SNAPSHOT'
println "appVersion[$appVersion]"

group = 'com.grupoesfera'
version = appVersion
sourceCompatibility = 1.8

def apiName = System.properties['apiName']?:'app'

println "apiName[$apiName]"
archivesBaseName = apiName


repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/libs-release' }
	maven { url 'https://oss.sonatype.org/content/groups/public' }
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-data-redis:2.0.0.RELEASE')
    compile('org.springframework.boot:spring-boot-starter-logging:2.0.0.RELEASE')
    compile('org.springframework.boot:spring-boot-starter-data-jpa:2.0.0.RELEASE')
	compile('com.microsoft.sqlserver:mssql-jdbc:6.4.0.jre8')

	compile('io.springfox:springfox-swagger-ui:2.8.0')
	compile('io.springfox:springfox-swagger2:2.8.0')
	compile('io.springfox:springfox-spring-web:2.8.0')

	dockerJava 'com.github.docker-java:docker-java:3.1.0-SNAPSHOT'
	dockerJava 'cglib:cglib:3.2.0'

	//TEST DEPENDENCIES
		testCompile('org.springframework.boot:spring-boot-starter-test')
		testCompile('org.spockframework:spock-spring:1.1-groovy-2.4')
	testCompile('cglib:cglib-nodep:3.2.2')
	runtime('com.h2database:h2:1.4.196')
}

// ===== Docker

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

task createDockerfile(type: Dockerfile) {
	dependsOn build
	destFile = project.file("${buildDir}/libs/Dockerfile")
	from 'frolvlad/alpine-oraclejdk8:slim'
	volume "/tmp"
	maintainer 'maty "matias.gonzalez@grupoesfera.com.ar"'
	addFile "./${archivesBaseName}-${version}.jar", "/app.jar"
	runCommand "sh -c 'touch /app.jar'"
	environmentVariable "JAVA_OPTS", '""'
	entryPoint "sh", "-c", "java \$JAVA_OPTS -Djava.security.egd=file:/dev/./urandom -jar /app.jar"
}

task buildImage(type: DockerBuildImage) {
	dependsOn createDockerfile
	inputDir = createDockerfile.destFile.parentFile
	tag = "${archivesBaseName}:${version}"
}
/*
task pushImage(type: DockerPushImage) {
	dependsOn buildImage
	imageName = buildImage.tag
	tag = app.version
}*/
// ===== End Docker
